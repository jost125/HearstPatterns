Phase:Hearst
Input:Token SpaceToken
Options: control = appelt

Macro:Noun
(
	({Token.category=="NNS"}|{Token.category=="NN"})
)

Macro:SingleNounPhrase
(
	(
		({Token.category=="JJ"}|{Token.category=="NNP"}|{Token.category=="NNPS"}|{Token.category=="IN"}|{Token.category=="VBG"})
		{SpaceToken.kind==space}
	)*
	(Noun)
)

Macro:NounPhrase
(
	(
		(
			{Token.category=="DT"}
			{SpaceToken.kind==space}
		)?
		(SingleNounPhrase)
	)
	(
		{SpaceToken.kind==space}
		(
			{Token.category=="DT"}
			{SpaceToken.kind==space}
		)?
		(SingleNounPhrase)
	)*
)

Macro:NounPhrasesAlternatives
(
	(NounPhrase)
	(
		({SpaceToken.kind==space})?
		{Token.kind==punctuation}
		({SpaceToken.kind==space})?
		(NounPhrase)
		
	)*
	(
		{SpaceToken.kind==space}
		({Token.string=="or"}|{Token.string=="and"})
		{SpaceToken.kind==space}
		(NounPhrase)
	)?
)

Rule:Hearst1
(
	(NounPhrase):hypernym
	(
		({SpaceToken.kind==space})?
		{Token.kind==punctuation}
	)?
	{SpaceToken.kind==space}
	{Token.string=="such"}
	{SpaceToken.kind==space}
	{Token.string=="as"}
	{SpaceToken.kind==space}
	(NounPhrasesAlternatives):hyponym
):hearst1
-->
:hearst1.Hearst={rule="Hearst1"},
:hyponym.Hyponym={rule="Hearst1"},
:hypernym.Hypernym={rule="Hearst1"}

