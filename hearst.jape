Phase:Hearst
Input:Token SpaceToken
Options: control = appelt

Macro:Noun
(
	(
		{Token.category=="NNS"}|
		{Token.category=="NN"}|
		{Token.category=="JJ"}|
		{Token.category=="NNP"}|
		{Token.category=="NNPS"}|
		{Token.category=="VBG"}|
		{Token.category=="CD"}
	)
	({Token.category=="POS"})?
)

Macro:SingleNounPhrase
(
	(
			({Token.category=="DT"}|{Token.category=="PRP$"})
			{SpaceToken.kind==space}
	)?
	(Noun)
)

Macro:NounPhrase
(
	(SingleNounPhrase)
	(
		(
			{SpaceToken.kind==space}
			{Token.category=="IN"}
		)?
		{SpaceToken.kind==space}
		(SingleNounPhrase)
	)*
)

Macro:Hypernym
(
	(NounPhrase)
)

Macro:AndOrNounPhrase
(
	{SpaceToken.kind==space}
	({Token.string=="or"}|{Token.string=="and"})
	{SpaceToken.kind==space}
	(NounPhrase)
)

Macro:PunctationNounPhrase
(
	({SpaceToken.kind==space})?
	{Token.kind==punctuation}
	({SpaceToken.kind==space})?
	(NounPhrase)
)

Macro:NounPhrasesAlternatives
(
	(NounPhrase)
	(PunctationNounPhrase)*
	(AndOrNounPhrase)?
)

Rule:Hearst1
(
	(Hypernym):hypernym
	(
		({SpaceToken.kind==space})?
		{Token.kind==punctuation}
	)?
	{SpaceToken.kind==space}
	{Token.string=="such"}
	{SpaceToken.kind==space}
	{Token.string=="as"}
	{SpaceToken.kind==space}
	(NounPhrasesAlternatives):hyponym
):hearst1
-->
:hearst1.Hearst={rule="Hearst1"},
:hyponym.Hyponym={rule="Hearst1"},
:hypernym.Hypernym={rule="Hearst1"}

Rule:Hearst2
(
	{Token.string==~"[Ss]uch"}
	{SpaceToken.kind==space}
	(Hypernym):hypernym
	{SpaceToken.kind==space}
	{Token.string=="as"}
	{SpaceToken.kind==space}
	(NounPhrasesAlternatives):hyponym
):hearst2
-->
:hearst2.Hearst={rule="Hearst2"},
:hyponym.Hyponym={rule="Hearst2"},
:hypernym.Hypernym={rule="Hearst2"}

Rule:Hearst3
(
	(
		(NounPhrase)
		(PunctationNounPhrase)*
		({Token.kind==punctuation})?
	):hyponym

	{SpaceToken.kind==space}
	({Token.string=="and"}|{Token.string=="or"})
	{SpaceToken.kind==space}
	{Token.string=="other"}
	{SpaceToken.kind==space}
	(Hypernym):hypernym
):hearst3
-->
:hearst3.Hearst={rule="Hearst3"},
:hyponym.Hyponym={rule="Hearst3"},
:hypernym.Hypernym={rule="Hearst3"}

Rule:Hearst4
(
	(
		(Hypernym)
		({Token.kind==punctuation})?
	):hypernym

	{SpaceToken.kind==space}
	({Token.string=="including"}|{Token.string=="especially"})
	{SpaceToken.kind==space}
	
	(
		(NounPhrase)
		(PunctationNounPhrase)*
		(AndOrNounPhrase)?
	):hyponym
):hearst4
-->
:hearst4.Hearst={rule="Hearst4"},
:hyponym.Hyponym={rule="Hearst4"},
:hypernym.Hypernym={rule="Hearst4"}
