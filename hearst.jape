Phase:Hearst
Input:Token SpaceToken
Options: control = appelt

Macro:Noun
(
	({Token.category=="NNS"}|{Token.category=="NN"}|{Token.category=="CD"})
)

Macro:NounPhraseWithAdjective
(
	(
		({Token.category=="JJ"}|{Token.category=="NNP"}|{Token.category=="NNPS"})
		{SpaceToken.kind==space}
	)*
	(Noun)
)

Macro:NounPhrase
(
	(
		(
			{Token.category=="DT"}
			{SpaceToken.kind==space}
		)?
		(NounPhraseWithAdjective)
	)
	(
		{SpaceToken.kind==space}
		(
			{Token.category=="DT"}
			{SpaceToken.kind==space}
		)?
		(NounPhraseWithAdjective)
	)*
)

Macro:NounPhrasesAlternatives
(
	(NounPhrase)
	(
		({SpaceToken.kind==space})?
		{Token.kind==punctuation}
		({SpaceToken.kind==space})?
		(NounPhrase)
		
	)*
	(
		{SpaceToken.kind==space}
		({Token.string=="or"}|{Token.string=="and"})
		{SpaceToken.kind==space}
		(NounPhrase)
	)?
)

Rule:Hearst1
(
	(NounPhrase):hypernym
	(
		({SpaceToken.kind==space})?
		{Token.kind==punctuation}
	)?
	{SpaceToken.kind==space}
	{Token.string==~"[Ss]uch"}
	{SpaceToken.kind==space}
	{Token.string==~"[Aa]s"}
	{SpaceToken.kind==space}

	(NounPhrasesAlternatives):hyponym
):hearst1
-->
:hearst1.Hearst={rule="Hearst1"},
:hyponym.Hyponym={rule="Hearst1"},
:hypernym.Hypernym={rule="Hearst1"}

